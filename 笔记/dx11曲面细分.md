# dx11曲面细分

## 1.简单介绍

曲面细分是DirectX11中新增的技术，可以通过该技术将几何体表面细分成更小的三角形，并且将细分出来的三角形顶点进行偏移，形成更加丰富的网格细节，以下是三种应用场景：

1. 基于GPU实现动态LOD（Level of Detail，细节级别）。可以根据网格与摄像机的距离或依据其他因素来调整其细节。比如说，若网格离摄像机过远，则按高模的规格对它进行渲染将是一种浪费，因为在那个距离我们根本看不清网格的所有细节。随着物体与摄像机之间距离的拉紧，我们就能连续地对它镶嵌细分，以增加物体的细节。
2. 物理模拟与动画特效。我们可以在低模（low-poly，低面数多边形）网格上执行物理模拟与动画特效的相关计算，再以镶嵌画处理手段来获取细节上更加丰富的网格。这种降低物理模拟与动画特效计算量的做法能够节省不少的计算资源。
3. 节约内存。我们可以在各种存储器（磁盘、RAM、VRAM）中保存低模网格，再根据需求用GPU动态地对网格进行镶嵌细分。

## 2.曲面细分阶段

曲面细分阶段是dx11渲染管线中顶点着色器与几何着色器之间的阶段，按先后顺序可分为三个部分：`外壳着色器->镶嵌器（镶嵌化）->域着色器`三个阶段，示意图如下：

![img](https://img2020.cnblogs.com/blog/1172605/202007/1172605-20200712011345894-515605456.png)

## 3.外壳着色器

外壳着色器分为两部分，**常量外壳着色器**和**控制点外壳着色器**

#### 0）补充

i.曲面细分使用的图元类型

在进行曲面细分时，我们并不向IA（输入装配）阶段提交三角形，而是提交具有若干**控制点**的**面片**，通过控制点绘制各个面片，图元类型的枚举值如下：

```cpp
D3D_PRIMITIVE_TOPOLOGY_1_CONTROL_POINT_PATCHLIST	= 33,
D3D_PRIMITIVE_TOPOLOGY_2_CONTROL_POINT_PATCHLIST	= 34,
D3D_PRIMITIVE_TOPOLOGY_3_CONTROL_POINT_PATCHLIST	= 35,
...
D3D_PRIMITIVE_TOPOLOGY_31_CONTROL_POINT_PATCHLIST	= 63,
D3D_PRIMITIVE_TOPOLOGY_32_CONTROL_POINT_PATCHLIST	= 64,
```

> 控制点：用于控制面片形状的点，数量越多对形状的操控越自由

举例：

对于三角形，可以将三角形看作是拥有3个控制点的三角形面片（`D3D_PRIMITIVE_TOPOLOGY_3_CONTROL_POINT_PATCHLIST`），所以我们依然可以提交需要镶嵌化处理的普通三角形网格。

对于简单的四边形面片而言，则只需要提交4个控制点的面片（`D3D_PRIMITIVE_4_CONTROL_POINT_PATCH`）即可。这些面片最终也会在曲面细分阶段经过镶嵌化处理而分解为多个三角形。

> 注意：`D3D_PRIMITIVE_TOPOLOGY`枚举项描述输入装配阶段中的顶点类型，而`D3D_PRIMITIVE`枚举项则描述的是外壳着色器的输入图元类型。

ii.细分因子

细分因子分为边缘细分因子和内部细分因子，两者的输出系统值分别为`SV_TessFactor`和`SV_InsideTessFactor`，两种类型均可为float或float数组类型，取决于输入装配阶段的图元类型

#### 1）常量外壳着色器

常量外壳着色器每处理一个面片就会调用一次，作用是输出当前面片的细分因子（包括边缘细分因子和内部细分因子），也可以为其添加其它值，用于输出所需的数据，一种细分四边形面的常量外壳着色器示例如下：

```cpp
struct QuadPatchTess
{
    float EdgeTess[4] : SV_TessFactor;
    float InsideTess[2] : SV_InsideTessFactor;
    
    // 可以在下面为每个面片附加所需的额外信息
};

QuadPatchTess QuadConstantHS(InputPatch<VertexOut, 4> patch, uint patchID : SV_PrimitiveID)
{
    QuadPatchTess pt;
    
    pt.EdgeTess[0] = g_QuadEdgeTess[0];			// 四边形面片的左侧边缘
    pt.EdgeTess[1] = g_QuadEdgeTess[1];			// 四边形面片的上侧边缘
    pt.EdgeTess[2] = g_QuadEdgeTess[2];			// 四边形面片的右侧边缘
    pt.EdgeTess[3] = g_QuadEdgeTess[3];			// 四边形面片的下册边缘
    pt.InsideTess[0] = g_QuadInsideTess[0];		// u轴(四边形内部细分的列数)
    pt.InsideTess[1] = g_QuadInsideTess[1];		// v轴(四边形内部细分的行数)
    
    return pt;
}
```

着色器的参数列表解释如下：

1.`InputPatch<VertexOut, 4>`定义了控制点的数目和信息，这里通过结构体`VertexOut`定义控制点信息，参数4为输入的控制点数量。

2.`uint patchID : SV_PrimitiveID`是通过系统值` SV_PrimitiveID`语义提供的面片ID，标识了绘制调用过程中的各个面片。

#### 2）控制点外壳着色器

控制点外壳着色器以常量外壳着色器输出的控制点作为输入，并输出新的控制点，且输入和输出的控制点数可以相同也可以不同。顶点着色器每输出一个控制点，控制点外壳着色器都会调用一次。一种简单的控制点外壳着色器示例如下：

```cpp
struct VertexOut
{
    float3 PosL : POSITION;
};

typedef VertexOut HullOut;

// Tessellation_Quad_Integer_HS.hlsl

[domain("quad")]
[partitioning("integer")]
[outputtopology("triangle_cw")]
[outputcontrolpoints(4)]
[patchconstantfunc("QuadConstantHS")]
[maxtessfactor(64.0f)]
float3 HS(InputPatch<VertexOut, 4> patch, uint i : SV_OutputControlPointID, uint patchId : SV_PrimitiveID) : POSITION
{
    return patch[i].PosL;
}
```

对于着色器的参数列表，解释如下：

1.`InputPatch<VertexOut, 4> patch`类似于常量外壳着色器的输入参数

2.`uint i : SV_OutputControlPointID`是当前正在工作的外壳着色器所输出控制点的ID，通过语义`SV_OutputControlPointID`标识每一个输出的控制点

3.`uint patchId : SV_PrimitiveID`是面片的ID，通过语义`SV_PrimitiveID`用于标识每一个面片

对于着色器的属性，解释如下：

1.`domain`：面片的类型。可选用的参数有`tri`（三角形面片）、`quad`（四边形面片）或`isoline`（等值线）

2.`partioning`：指定了曲面细分的细分模式。

- `integer`：新顶点的添加或移除依据的是**上取整的函数**。例如我们将细分值设为3.25f时，实际上它将会细分为4份。这样一来，在网格随着曲面细分级别而改变时，会容易发生明显的跃变。
- 非整型曲面细分（`fractional_even`/`fractional_odd`）：新顶点的增加或移除取决于曲面细分因子的整数部分，但是细微的渐变“过渡”调整就要根据细分因子的小数部分。当我们希望将粗糙的网格经曲面细分而平滑地过渡到具有更加细节的网格时，该参数就派上用场了。
- `pow2`：目前测试的时候行为和`integer`一致...

3.`outputtopology`：通过细分所创的三角形的绕序

- `triangle_cw`：顺时针方向的绕序
- `triangle_ccw`：逆时针方向的绕序
- `line`：针对线段的曲面细分

4.`outputcontrolpoints`：外壳着色器执行的次数，每次执行都输出1个控制点。系统值`SV_OutputControlPointID`给出的索引标明了当前正在工作的外壳着色器所输出的控制点。

5.`patchconstantfunc`：指定常量外壳着色器函数名称的字符串

6.`maxtessfactor`：告知驱动程序，用户在着色器中所用的曲面细分因子的最大值。如果硬件知道了此上限，就可以了解曲面细分所需的资源，继而在后台对此进行优化。Direct3D 11硬件支持的曲面细分因子最大值为64

## 4.镶嵌化阶段

本阶段完全由硬件处理，属于不可编程阶段，负责根据外壳着色器输出的曲面细分因子对面片进行镶嵌化处理。示例略

## 5.域着色器

域着色器是曲面细分阶段中的最后一个阶段，是通过外壳着色器输出的控制点和细分因子、镶嵌化阶段新建的顶点位置（重心坐标系形式）作为参数输入，对面片做进一步处理的阶段。

对于三角形的曲面细分，域着色器示例代码为：

```cpp
struct VertexOut
{
    float3 PosL : POSITION;
};

typedef VertexOut HullOut;

// Tessellation_Triangle_DS.hlsl

[domain("tri")]
float4 DS(TriPatchTess patchTess,
    float3 weights : SV_DomainLocation,
    const OutputPatch<HullOut, 3> tri) : SV_POSITION
{
    // 重心坐标系插值
    float3 pos = tri[0].PosL * weights[0] +
        tri[1].PosL * weights[1] +
        tri[2].PosL * weights[2];
    
    return float4(pos, 1.0f);
}

```

对于四边形的曲面细分，使用的位置参数以（u，v）表示

```cpp
struct VertexOut
{
    float3 PosL : POSITION;
};

typedef VertexOut HullOut;

// Tessellation_Quad_DS.hlsl

[domain("quad")]
float4 DS(QuadPatchTess patchTess,
    float2 uv : SV_DomainLocation,
    const OutputPatch<HullOut, 4> quad) : SV_POSITION
{
    // 双线性插值
    float3 v1 = lerp(quad[0].PosL, quad[1].PosL, uv.x);
    float3 v2 = lerp(quad[2].PosL, quad[3].PosL, uv.x);
    float3 p = lerp(v1, v2, uv.y);
    
    return float4(p, 1.0f);
}
```

可以发现，域着色器的参数列表可表示如下：

```
返回值 着色器名称（细分因子，镶嵌化后的顶点坐标，输出控制点信息）
```

且可通过属性`domain`控制输出面元类型。